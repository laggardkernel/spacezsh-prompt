#!/usr/bin/env zsh
# vim: ft=zsh fdm=marker foldlevel=0 sw=2 ts=2 sts=2 et

function prompt_spacezsh_help {
  cat <<'EOF'
ðŸš€â­ï¸ An async prompt does things in the ZSH way.

  prompt spacezsh

Spacezsh is a minimalistic, powerful and extremely customizable Z Shell prompt.
It combines everything you may need for convenient work, without unnecessary complications.
EOF
}

# ------------------------------------------------------------------------------
# SETUP
# Setup requirements for prompt
# ------------------------------------------------------------------------------

# Runs once when user opens a terminal
# All preparation before drawing prompt should be done here
function prompt_spacezsh_setup {
  # reset prompt according to prompt_default_setup
  PS1='%m%# '
  PS2='%_> '
  PS3='?# '
  PS4='+%N:%i> '
  unset RPS1 RPS2 RPROMPT RPROMPT2

  setopt localoptions KSH_GLOB
  local item

  # This variable is a magic variable used when loading themes with zsh's prompt
  # function. It will ensure the proper prompt options are set.
  prompt_opts=(cr percent sp subst)

  # Borrowed from promptinit, sets the prompt options in case the prompt was not
  # initialized via promptinit.
  setopt noprompt{bang,cr,percent,subst} "prompt${^prompt_opts[@]}"

  # initialize timing functions
  zmodload zsh/datetime

  # Initialize math functions
  zmodload zsh/mathfunc

  typeset -gAH _SS_DATA=()

  # Internal variable for checking if prompt is opened
  _SS_DATA[section_var_file]="${XDG_CACHE_HOME:-$HOME/.cache}/spacezsh_section_var.zsh"

  # store result from sz::section
  _SS_DATA[section_result]=""

  typeset -gAh _sz_async_sections=()
  typeset -gAh _sz_custom_sections=()

  # Global section cache
  typeset -gAh _sz_section_cache=()

  # _sz_unsafe must be a global variable, because we set
  # PROMPT='$_sz_unsafe[left]', so without letting ZSH
  # expand this value (single quotes). This is a workaround
  # to avoid double expansion of the contents of the PROMPT.
  typeset -gAh _sz_unsafe=()

  # Load a preset by name
  # presets must be loaded before lib/default.zsh
  if [[ -n "$1" ]]; then
    if [[ -r "${SPACESHIP_ROOT}/presets/${1}.zsh" ]]; then
      source "${SPACESHIP_ROOT}/presets/${1}.zsh"
    else
      echo "Spacezsh: No such preset found in ${SPACESHIP_ROOT}/presets/${1}"
    fi
  fi

  builtin autoload -Uz +X -- add-zsh-hook track-sz-autoload
  track-sz-autoload add-sz-hook
  builtin autoload -Uz +X -- run-sz-hook && _SS_AUTOLOADED+=(run-sz-hook)

  # setup functions
  for item in "${SPACESHIP_ROOT}"/lib/setups/sz::!(*.zwc)(-.N:t); do
    track-sz-autoload "$item"
  done

  # Utilities
  for item in "${SPACESHIP_ROOT}"/lib/utils/sz::!(*.zwc)(-.N:t); do
    track-sz-autoload "$item"
  done

  # Always load default conf
  source "$SPACESHIP_ROOT/presets/default.zsh"
  # LIBS
  source "$SPACESHIP_ROOT/lib/utils.zsh"
  # sections.zsh is deprecated, cause it's linked as prompt_spacezsh_setup
  # source "$SPACESHIP_ROOT/lib/section.zsh"
  sz::load_sections
  sz::deprecated_check

  # Hooks
  track-sz-autoload prompt_spacezsh_precmd prompt_spacezsh_preexec \
    prompt_spacezsh_chpwd

  add-zsh-hook precmd prompt_spacezsh_precmd
  add-zsh-hook preexec prompt_spacezsh_preexec
  # hook into chpwd for bindkey support
  add-zsh-hook chpwd prompt_spacezsh_chpwd
  # redraw prompt once pwd is changed in zle widgets
  add-zsh-hook chpwd prompt_spacezsh_precmd

  # The value below was set to better support 32-bit CPUs.
  # It's the maximum _signed_ integer value on 32-bit CPUs.
  # Please don't change it until 19 January of 2038. ;)

  # Disable false display of command execution time
  _SS_DATA[time_start]=0x7FFFFFFF

  # Disable python virtualenv environment prompt prefix
  VIRTUAL_ENV_DISABLE_PROMPT=true

  # Expose Spaceship to environment variables
  PS2='$(sz::ps2)'

  # prepend custom cleanup hook
  local -a cleanup_hooks
  zstyle -g cleanup_hooks :prompt-theme cleanup
  zstyle -e :prompt-theme cleanup "prompt_spacezsh_cleanup;" "${cleanup_hooks[@]}"
  # append cleanup hook with builtin func
  # prompt_cleanup "prompt_spacezsh_cleanup"
}

# TODO: compile helper

# This function removes spacezsh hooks and resets the prompts.
function prompt_spacezsh_cleanup {
  local item

  async_stop_worker "spacezsh_section_worker" 2>/dev/null

  # prmopt specific cleanup
  sz::vi_mode_disable 2>/dev/null
  sz::vi_mode_cleanup 2>/dev/null
  # TODO: cleanup preset conf

  # prompt hooks
  add-zsh-hook -D chpwd   prompt_spacezsh_\*
  add-zsh-hook -D precmd  prompt_spacezsh_\*
  add-zsh-hook -D preexec prompt_spacezsh_\*

  # setopt localoptions NULL_GLOB
  for item in ${_SS_AUTOLOADED[@]}; do
    builtin unfunction -- "$item"
  done

  unset _sz_async_sections _sz_custom_sections _sz_section_cache _sz_unsafe \
    sz_{chpwd,precmd,preexec}_functions \
    _SS_DATA _SS_AUTOLOADED 2>/dev/null

  # reset prompt according to prompt_default_setup
  PS1='%m%# '
  PS2='%_> '
  PS3='?# '
  PS4='+%N:%i> '
  unset RPS1 RPS2 RPROMPT RPROMPT2
  prompt_opts=( cr percent sp )
}

prompt_spacezsh_setup "$@"
