#!/usr/bin/env zsh
# vim: ft=zsh fdm=marker foldlevel=0 sw=2 ts=2 sts=2 et

#
# PHP
#
# PHP is a server-side scripting language designed primarily for web development.
# Link: http://www.php.net/

# ------------------------------------------------------------------------------
# Configuration
# ------------------------------------------------------------------------------

SPACESHIP_PHP_SHOW="${SPACESHIP_PHP_SHOW=true}"
SPACESHIP_PHP_PREFIX="${SPACESHIP_PHP_PREFIX="$SPACESHIP_PROMPT_DEFAULT_PREFIX"}"
SPACESHIP_PHP_SUFFIX="${SPACESHIP_PHP_SUFFIX="$SPACESHIP_PROMPT_DEFAULT_SUFFIX"}"
SPACESHIP_PHP_SYMBOL="${SPACESHIP_PHP_SYMBOL="ðŸ˜ "}"
SPACESHIP_PHP_COLOR="${SPACESHIP_PHP_COLOR="blue"}"

# ------------------------------------------------------------------------------
# Section
# ------------------------------------------------------------------------------

# Show current version of PHP
sz::php() {
  [[ $SPACESHIP_PHP_SHOW == false ]] && return

  # Show only if php files or composer.json exist in current directory
  sz::upsearch "composer.json" >/dev/null \
    || [[ -n *.php(#qN^/) ]] \
    || return

  (( $+commands[php] )) || return

  local php_version=$(php -v 2>&1 | command grep --color=never -oe "^PHP\s*[0-9.]\+" | awk '{print $2}')

  sz::section \
    "$SPACESHIP_PHP_COLOR" \
    "${SPACESHIP_PHP_SYMBOL}v${php_version}" \
    "$SPACESHIP_PHP_PREFIX" \
    "${SPACESHIP_PHP_SUFFIX}"
}

sz::php "$@"
