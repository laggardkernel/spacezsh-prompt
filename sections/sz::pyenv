#!/usr/bin/env zsh
# vim: ft=zsh fdm=marker foldlevel=0 sw=2 ts=2 sts=2 et

#
# pyenv
#
# pyenv lets you easily switch between multiple versions of Python.
# Link: https://github.com/pyenv/pyenv

# ------------------------------------------------------------------------------
# Configuration
# ------------------------------------------------------------------------------

SPACESHIP_PYENV_SHOW="${SPACESHIP_PYENV_SHOW=true}"
SPACESHIP_PYENV_PREFIX="${SPACESHIP_PYENV_PREFIX="$SPACESHIP_PROMPT_DEFAULT_PREFIX"}"
SPACESHIP_PYENV_SUFFIX="${SPACESHIP_PYENV_SUFFIX="$SPACESHIP_PROMPT_DEFAULT_SUFFIX"}"
SPACESHIP_PYENV_SYMBOL="${SPACESHIP_PYENV_SYMBOL="ðŸ "}"
SPACESHIP_PYENV_COLOR="${SPACESHIP_PYENV_COLOR="yellow"}"

# ------------------------------------------------------------------------------
# Section
# ------------------------------------------------------------------------------

# Show current version of pyenv Python, including system.
sz::pyenv() {
  [[ $SPACESHIP_PYENV_SHOW == false ]] && return

  (( $+commands[pyenv] )) || return # Do nothing if pyenv is not installed

  # Show pyenv python version only for Python-specific folders
  [[ -n "$PYENV_VERSION" ]] \
    || sz::upsearch ".python-version" >/dev/null \
    || sz::upsearch "requirements.txt" >/dev/null \
    || sz::upsearch "pyproject.toml" >/dev/null \
    || [[ -n *.py(#qN^/) ]] \
    || return

  local pyenv_status=${$(pyenv version-name 2>/dev/null)//:/ }

  sz::section \
    "$SPACESHIP_PYENV_COLOR" \
    "${SPACESHIP_PYENV_SYMBOL}${pyenv_status}" \
    "$SPACESHIP_PYENV_PREFIX" \
    "$SPACESHIP_PYENV_SUFFIX"
}

sz::pyenv "$@"
